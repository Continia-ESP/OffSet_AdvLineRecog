OBJECT Table 50010 CDC Offset Setup
{
  OBJECT-PROPERTIES
  {
    Date=10/06/21;
    Time=10:07:56;
    Modified=Yes;
    Version List=;
  }
  PROPERTIES
  {
  }
  FIELDS
  {
    { 1   ;   ;Template No         ;Code20        ;DataClassification=ToBeClassified }
    { 5   ;   ;Field Code          ;Code20        ;DataClassification=ToBeClassified }
    { 10  ;   ;Fixed               ;Boolean       ;DataClassification=ToBeClassified }
    { 15  ;   ;Top                 ;Integer       ;DataClassification=ToBeClassified }
    { 20  ;   ;Left                ;Integer       ;DataClassification=ToBeClassified }
    { 25  ;   ;Bottom              ;Integer       ;DataClassification=ToBeClassified }
    { 30  ;   ;Right               ;Integer       ;DataClassification=ToBeClassified }
  }
  KEYS
  {
    {    ;Template No,Field Code                  ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    BEGIN
    END.
  }
}

OBJECT Codeunit 60002 Line Capture - Offset 2
{
  OBJECT-PROPERTIES
  {
    Date=10/06/21;
    Time=10:14:10;
    Modified=Yes;
    Version List=DC;
  }
  PROPERTIES
  {
    TableNo=6085590;
    OnRun=BEGIN
            Code(Rec);
          END;

  }
  CODE
  {
    VAR
      CaptureMgt@1160040001 : Codeunit 6085576;

    PROCEDURE Code@1000000000(VAR Document@1000000000 : Record 6085590);
    VAR
      Value@1210050001 : Record 6085593;
      TempDocLine@1000000001 : TEMPORARY Record 6085596;
      LineNoField@1210050000 : Record 6085580;
      OffsetSetup@1100288000 : Record 50010;
    BEGIN
      //RUN STANDARD LINE CAPTURING CODEUNIT
      CODEUNIT.RUN(CODEUNIT::"CDC Purch./Sales - Line Capt.",Document);

      //GET FIELD FOR FIXED POSITION
      OffsetSetup.SETRANGE("Template No",Document."Template No.");
      OffsetSetup.SETRANGE(Fixed,TRUE);
      IF NOT OffsetSetup.FINDFIRST THEN
        EXIT;

      LineNoField.GET(Document."Template No.",LineNoField.Type::Line,OffsetSetup."Field Code");

      //BUILD TEMPORARY LINE TABLE AND LOOP LINES
      Document.BuildTempLinesTable(TempDocLine);
      IF TempDocLine.FINDSET THEN
        REPEAT
          //STORE THE NO. VALUE INTO A DOCUMENT VALUE RECORD (WHICH CONTAINS POSITIONS)
          CaptureMgt.GetFieldValue(Document,LineNoField,TempDocLine."Line No.",Value);

          OffsetSetup.SETRANGE(Fixed,FALSE);
          IF OffsetSetup.FINDSET THEN
            REPEAT
              //GET THE QUANTITY AND UNIT COST VALUES WITH THE DOCUMENT VALUE POSITIONS
              CaptureValue(LineNoField,TempDocLine,OffsetSetup."Field Code",Value.Top + OffsetSetup.Top,Value.Left + OffsetSetup.Left,Value.Bottom + OffsetSetup.Bottom,Value.Right + OffsetSetup.Right);
            UNTIL OffsetSetup.NEXT = 0;
        UNTIL TempDocLine.NEXT = 0;
    END;

    PROCEDURE CaptureValue@1210050000(VAR Field@1210050000 : Record 6085580;TempDocLine@1160040000 : TEMPORARY Record 6085596;FieldCode@1210050001 : Code[20];Top@1210050005 : Integer;Left@1210050006 : Integer;Bottom@1210050008 : Integer;Right@1210050007 : Integer);
    VAR
      OffsetField@1210050004 : Record 6085580;
      Value@1160040007 : Record 6085593;
      Page@1210050003 : Record 6085591;
    BEGIN
      Page.GET(TempDocLine."Document No.",TempDocLine."Page No.");

      OffsetField.GET(Field."Template No.",OffsetField.Type::Line,FieldCode);

      CaptureMgt.CaptureFromPos(Page,OffsetField,TempDocLine."Line No.",TRUE,Top,Left,Bottom,Right,Value)
    END;

    BEGIN
    END.
  }
}

